{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Methods for creating / working with tree-shaped networks to demonstrate / benchmark\n",
    "# functionality for PixelArrayPowerFlow\n",
    "push!(LOAD_PATH, \"../jlfiles/\")\n",
    "\n",
    "using PixelArrayPowerFlow: Network, Branch, Bus, Gen, SlackParams, PQParams\n",
    "using PixelArrayPowerFlow\n",
    "\n",
    "using Plots"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Creates a sample binary tree network with given height and 2^(height + 1) - 1 nodes\n",
    "# The very top node is a slack bus, the rest are all generic PQ buses\n",
    "function createbinarytreenetwork(height::Int)\n",
    "    if height < 0\n",
    "        error(\"Height must be a nonnegative integer.\")\n",
    "    end\n",
    "    \n",
    "    # Create empty arrays of branches, buses, gens\n",
    "    branches = Array{Branch}(undef, 2^(height+1) - 2)\n",
    "    buses = Array{Bus}(undef, 2^(height+1) - 1)\n",
    "    gens = Array{Gen}(undef, 1)\n",
    "\n",
    "    # Create a head node for the network (slack bus with a generator attached)\n",
    "    (Pmin, Pmax) = (-1.0, 1.0)\n",
    "    (Qmin, Qmax) = (-1.0, 1.0)\n",
    "    (Vmin, Vmax) = (-1.0, 1.0) # this doesn't actually matter to the problem\n",
    "    (θmin, θmax) = (-π/6, π/6) # this doesn't actually matter to the problem\n",
    "    (Bshunt, Gshunt) = (0.0, 0.0)\n",
    "    (V, θ) = (1.0, 0.0)\n",
    "    id = 1\n",
    "    gen = Gen(Pmin, Pmax, Qmin, Qmax)\n",
    "    slackparams = SlackParams(V, θ)\n",
    "    head = Bus(id, Vmin, Vmax, θmin, θmax, Bshunt, Gshunt, gen, slackparams)\n",
    "\n",
    "    # Add head node, generator to array\n",
    "    gens[1] = gen\n",
    "    buses[1] = head\n",
    "\n",
    "    # Starting from the very top, use recursion to create other buses\n",
    "    # (and branches linking the other buses to the nodes directly above)\n",
    "    # and populate branch / bus arrays with these\n",
    "    binarytreeaux!(height, head, branches, buses)\n",
    "\n",
    "    # Combine these into a network structure\n",
    "    basemva = 100.0\n",
    "    network = Network(branches, buses, gens, basemva)\n",
    "\n",
    "    return network\n",
    "end\n",
    "\n",
    "# Recursive auxiliary function for createbinarytreenetwork. If curr is not a leaf then\n",
    "# create children for curr and branches connecting curr to children, adding both to arrays.\n",
    "# Then continue for each child with a reduced height.\n",
    "# (branches and buses are the only arguments modified in the process)\n",
    "function binarytreeaux!(height::Int, curr::Bus, branches::Array{Branch,1}, buses::Array{Bus,1})\n",
    "    if height != 0\n",
    "        # curr is not a leaf, so create ids for children\n",
    "        # These ids double as indices for the arrays\n",
    "        childrenids = (2*curr.id, 2*curr.id + 1)\n",
    "        for i in childrenids\n",
    "            # Create child, branch connecting child to curr\n",
    "            buses[i] = genericPQbus(i)\n",
    "            branches[i-1] = genericbranch(curr, buses[i])\n",
    "\n",
    "            # Continue recursion on just-created child\n",
    "            binarytreeaux!(height-1, buses[i], branches, buses)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    return nothing # function only used for side effects\n",
    "end\n",
    "\n",
    "# Returns a generic PQ bus with given ID, power consumption 1.0 W\n",
    "function genericPQbus(id::Int)\n",
    "    return Bus(id, -1.0, 1.0, -π/6, π/6, 0.0, 0.0, nothing, PQParams(1.0, 0.0))\n",
    "end\n",
    "\n",
    "# Creates a generic branch with given source / target, no impedance\n",
    "function genericbranch(frombus::Bus, tobus::Bus)\n",
    "    return Branch(frombus, tobus, 0.0, 0.0, 0.0, 0.0, 0.0)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "heights = 10\n",
    "nrepetitions = 3\n",
    "\n",
    "# Generic variables for later use\n",
    "allres = 2\n",
    "tol = 1.0\n",
    "psp = PackSetupParams(fill(allres,4)...)\n",
    "\n",
    "times = Array{Float64}(undef, heights)\n",
    "\n",
    "for i = 1:heights\n",
    "    network = createbinarytreenetwork(i)\n",
    "    \n",
    "    totaltime = @elapsed begin\n",
    "        for j = 1:nrepetitions\n",
    "            pwas = getpfpwas(network, psp, tol)\n",
    "            # Perform final array multiplication, leaving P1 and Q1 unexposed\n",
    "            finalpwa = clustertree(network, pwas, 1)\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    times[i] = totaltime / nrepetitions # take average\n",
    "end\n",
    "\n",
    "println(times)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "buscounts = map(i -> 2^(i+1) - 1, 1:heights)\n",
    "plt = plot(buscounts, times, scale=:log10)\n",
    "plot!(plt, title = \"Buses vs. Time to Solve\", xlabel = \"Number of buses\", ylabel = \"Average time\", legend=:none)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.0.3",
   "language": "julia",
   "name": "julia-1.0"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.0.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
